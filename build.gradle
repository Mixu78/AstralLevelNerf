buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'java'
}

apply plugin: 'net.minecraftforge.gradle'

group = 'mixu'
version = '1.0.0'
archivesBaseName = 'astral-level-nerf'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

repositories {
    maven {
        name 'hellfirepvp maven'
        url 'https://maven.hellfiredev.net'
    }
    maven {
        name 'cursemave'
        url 'https://cursemaven.com'
    }
}

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                astrallevelnerf {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                astrallevelnerf {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {

    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'

    compile 'hellfirepvp.astralsorcery:astralsorcery:1.12.2-1.10.25-v167'
    compile 'com.fathzer:javaluator:3.0.3'
    runtime 'curse.maven:baubles-227083:2518667'
}

shadowJar {
    dependencies {
        include(dependency('com.fathzer:javaluator:3.0.3'))
    }

    relocate 'com.fathzer', 'mixu.astrallevelnerf.repack.com.fathzer'
}

reobf {
    shadowJar {}
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                "Specification-Title"     : "astrallevelnerf",
                "Specification-Vendor"    : "Mixu_78",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "Mixu_78",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
        attributes 'Class-Path': '/mixu/astrallevelnerf/repack'
        attributes 'FMLCorePlugin': "mixu.astrallevelnerf.asm.AstralLevelNerfPlugin"
        attributes 'FMLCorePluginContainsFMLMod': true
    }
}

jar.finalizedBy('reobfJar')
